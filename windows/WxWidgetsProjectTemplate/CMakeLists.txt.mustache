cmake_minimum_required(VERSION 3.13)

project({{PROJECT_NAME}}
	LANGUAGES CXX
)

list(APPEND CMAKE_MODULE_PATH "${CMAKE_SOURCE_DIR}/cmake/modules")

find_package(wxWidgets REQUIRED
	COMPONENTS
		core
		base
		html
		xml
		xrc
)

# The GUI layout is defined in this generated source file.
set(LAYOUT_FILES "${CMAKE_CURRENT_BINARY_DIR}/main_window.cpp")

add_executable({{PROJECT_NAME}}
	${LAYOUT_FILES}
	include/{{PROJECT_NAME}}.hpp
	src/{{PROJECT_NAME}}.cpp
)
target_compile_features({{PROJECT_NAME}}
	PRIVATE
		cxx_std_17
)
target_include_directories({{PROJECT_NAME}}
	PRIVATE
		include
		${wxWidgets_INCLUDE_DIRS}
)
target_link_libraries({{PROJECT_NAME}}
	PRIVATE
		${wxWidgets_LIBRARIES}
)
target_compile_options({{PROJECT_NAME}}
	PRIVATE
		$<$<CXX_COMPILER_ID:Clang,AppleClang,GNU>:
			"-Wall -Wextra -pedantic-errors -pedantic"
		>
		${wxWidgets_CXX_FLAGS}
)
target_compile_definitions({{PROJECT_NAME}}
	PRIVATE
		${wxWidgets_DEFINITIONS}
)
if(WIN32)
	set_target_properties({{PROJECT_NAME}}
		PROPERTIES
			WIN32_EXECUTABLE True
	)
endif()

add_custom_command(
	OUTPUT ${LAYOUT_FILES}
	MAIN_DEPENDENCY resources/main_window.xrc
	COMMAND wxrc resources/main_window.xrc -c -v -o "${LAYOUT_FILES}"
	WORKING_DIRECTORY "${CMAKE_CURRENT_SOURCE_DIR}"
	COMMENT "Processing wxWidgets resource"
)